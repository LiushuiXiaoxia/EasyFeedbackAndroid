apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def properties = new Properties()
properties.load(new FileInputStream("${rootDir.absolutePath}/local.properties"))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "cn.mycommons.easyfeedback.example"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        manifestPlaceholders = [
                QINIU_AK: properties.getProperty("QINIU_AK", "aa"),
                QINIU_SK: properties.getProperty("QINIU_SK", "bb")
        ]
        buildConfigField("String", "UPLOAD_SERVER", "\"http://192.168.237.23:8081\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.properties'
        }
    }
}

configurations {
    // 所有插件排除 fragment 依赖
    all*.exclude group: 'com.android.support', module: 'support-fragment'

    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "com.android.support") {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "26.+"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.0.0'

    implementation project(':feedback')
    implementation project(":feedback-timber") // 可选插件
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // implementation 'com.qiniu:qiniu-android-sdk:7.4.3' // 可选插件


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
